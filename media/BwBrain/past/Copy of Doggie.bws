predicate name(string)
predicate speed(float)
predicate time(float)
predicate loop(bool)

class Doggie()
    #Random Forward Movement
    def RandomMove(random-move :name $n :speed $s :time $t)
        parallel
            translate :speed $s :time $t
            animate :name $n :speed 1.0 :loop true
            wait :time $t

    #Random Turning
    def RandomTurn(random-turn :time $t)
        probability-select
            case 1.0
                parallel
                    rotate :degree 1.0
                    animate :name "TurnLeft" :speed 1.0 :loop true
                    wait :time $t :random $t
            case 1.0    
                parallel
                    rotate :degree -1.0
                    animate :name "TurnRight" :speed 1.0 :loop true
                    wait :time $t :random $t

    #Passive Dog Behaviors
    def Passive(passive)
        probability-select
            case 1.0    
                animate :name "Sitting" :speed .34 :loop false
            case 1.0    
                animate :name "LayingIdle" :speed .67 :loop false

    #Active Dog Behaviors
    def Active(active)
        probability-select
            case 1.0    
                fail #fail occasionally - give other behaviors a chance.
            case .25
                animate :name "ChaseTail" :speed 1.0 :loop true
            case 5.0    
                random-turn :degree 1.0
                random-move :speed 4.0

    #Bodily Behaviors
    def Bodily(bodily)
        probability-select
            case 20.0
                fail #fail occasionally - give other behaviors a chance.
            case 2.0
                animate :name "LayingIdle" :speed .2 :loop false
                animate :name "Sleeping" :speed .2 :loop false
            case 4.0
                random-turn :time 1.5
                random-move :name "StalkingPrey" :speed 8.0 :time 0.5
                animate :name "Peeing" :speed 1.0 :loop false
                random-turn :time 1.5
                random-move :name "JumpyRun" :speed 2.0 :time 6.0
            case 4.0
                animate :name "ScratchingBack" :speed 1.0 :loop false
                animate :name "Scratching" :speed 1.0 :loop false
            
    #Root Behavior
    def Behave(behave)
        priority-select :repeat true
            bodily
            active
            passive
